module generating/webdsl/types

imports // constructors

  webdsl/constructors
  webdsl/WebDSL
  signatures/Types-sig
  desugaring/constructors

imports // functions

  api/_runtime
  names/naming/names
  naming/_notNaBL
  
rules // primitives
  
  type-to-webdsl-srt(err): Boolean() -> webdsl |[ Bool ]|      //webdsl srt
  type-to-webdsl-srt(err): Int()     -> webdsl |[ Int ]|      //webdsl srt  
  type-to-webdsl-srt(err): String()  -> webdsl |[ String ]|    //webdsl srt
  type-to-webdsl-srt(err): Float()   -> webdsl |[ Float ]|    //webdsl srt
  type-to-webdsl-srt(err): Datetime()-> webdsl |[ DateTime ]| //webdsl srt
  type-to-webdsl-srt(err): NoValue() -> webdsl |[ Void ]|
  type-to-webdsl-srt(err): name      -> name
    where is-string

rules // types of expressions and names (uses type and multiplicity)
  
  expr-to-webdsl-srt(err) = name-to-webdsl-srt(err)
  
  name-to-webdsl-srt(err): name -> result
    with
      srt1 := <get-type2;type-to-webdsl-srt(err)>name
    with
      if <get-multiplicity;upper-one>name then
      result := webdsl |[ //webdsl srt
        srt1
      ]|
    else
      result := webdsl |[ //webdsl srt
        [srt1]
      ]|
    end

rules // pritives as strings (not literal string in target language)

  type-to-string-webdsl: String()   -> "String"
  type-to-string-webdsl: Int()      -> "Integer"
  type-to-string-webdsl: Boolean()  -> "Boolean"
  type-to-string-webdsl: Float()    -> "Float"
  type-to-string-webdsl: Datetime() -> "DateTime"
  type-to-string-webdsl: name       -> name
    where is-string
